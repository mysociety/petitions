#!/usr/bin/perl -w
#
# petsignupd:
# Back-end signup service for petitions site.
#
# Clients -- the web scripts on the front-end servers -- send signup details to
# us in UDP packets. On receipt of such a packet we record that a signature is
# to be made, but do not make any change to the database. Instead we set an
# alarm for a short interval in the future. Any more packets that arrive before
# the alarm fires are also recorded; when the alarm fires, we insert them all,
# commit the changes, and acknowledge them to the senders.
#
# The idea here is that a commit of ten inserts takes about as long as a commit
# of one insert, so we can improve throughput by batching inserts. That's very
# inconvenient in the front-end scripts, because it means they'd have to share
# database handles, but is easy with a coordinating daemon. If the daemon fails
# or the packets don't get through, then the web scripts can sign up in the
# usual way.
#
# TODO: proper logging; make it a proper daemon; LRU cache of recent inserts so
# that we can immediately acknowledge resent requests from front end.
#
# Copyright (c) 2006 UK Citizens Online Democracy. All rights reserved.
# Email: chris@mysociety.org; WWW: http://www.mysociety.org/
#

my $rcsid = ''; $rcsid .= '$Id: petsignupd,v 1.11 2006-08-10 13:38:12 chris Exp $';

use strict;

# Horrible boilerplate to set up appropriate library paths.
use FindBin;
use lib "$FindBin::Bin/../perllib";
use lib "$FindBin::Bin/../../perllib";

use mySociety::Config;
BEGIN {
    mySociety::Config::set_file("$FindBin::Bin/../conf/general");
}

use Carp;
use Errno;
use Error qw(:try);
use IO::Select;
use IO::Socket;
use Socket;
use Tie::Cache;
use Time::HiRes qw(time alarm);

use mySociety::DBHandle qw(dbh);

use Petitions;
use Petitions::RPC;

# Maximum (ideal) interval from signup request to commit.
use constant MAX_SIGNUP_TIME    => 0.1;    # seconds

my $s = new IO::Socket::INET(
                    LocalAddr => '0.0.0.0',
                    LocalPort => mySociety::Config::get('PETSIGNUPD_PORT'),
                    Type => SOCK_DGRAM,
                    Proto => 'udp',
                    ReuseAddr => 1,
                    Blocking => 0) or die "socket: $!";

my $foad = 0;
$SIG{TERM} = sub { $foad = 1; };

my $alarm_pending = 0;
my $alarm_fired = 0;
$SIG{ALRM} = sub { $alarm_fired = 1; $alarm_pending = 0; };

my %cache;
tie(%cache, 'Tie::Cache', 5000);

# do_pending
# Actually perform the queue of requests against the database.
my %pending = ( );
sub do_pending () {
    my $t0 = time();
    
    foreach my $r (values(%pending)) {
        if (exists($r->{confirm})) {
            Petitions::RPC::confirm_db($r);
        } else {
            Petitions::RPC::sign_petition_db($r);
        }
    }
    dbh()->commit();

    my $t1 = time();
    printf STDERR "writing to db took %fs\n", $t1 - $t0;

    $s->blocking(1);
    my $maxtime = 0;
    foreach my $r (values(%pending)) {
        $cache{$r->{cookie}} = 1;
        my $packet = Petitions::RPC::make_ack_packet($r->{cookie});
        my ($port, $ip) = sockaddr_in($r->{client});
        if (!$s->send($packet, 0, $r->{client})) {
            print STDERR "sending acknowledgment to @{[ inet_ntoa($ip) ]}:$port: $!\n";
        } else {
            $maxtime = time() - $r->{time} if (time() - $r->{time} > $maxtime);
#            print STDERR "send ack to @{[ inet_ntoa($ip) ]}:$port cookie @{[ unpack('h*', $r->{cookie}) ]}\n";
        }
    }
    $s->blocking(0);

    printf STDERR "sending acks took %fs; max %fs since received req\n", time() - $t1, $maxtime;
}

my $t0;
while (!$foad) {
    my $buf = '';
    my $sender;
    my @ready = IO::Select->new($s)->can_read(MAX_SIGNUP_TIME / 10);
    if (@ready) {
        while (defined($sender = $s->recv($buf, 65536, 0))) {
            if (my $r = Petitions::RPC::parse_packet($buf)) {
                # We have a valid request. Schedule an alarm so that we will
                # send a response within the deadline.
                if (!$alarm_pending) {
                    $t0 = time();
                    $alarm_pending = 1;
                    alarm(MAX_SIGNUP_TIME);
                }

                # Record address to which we send acknowledgement.
                $r->{client} = $sender;
                $r->{time} = time();

                # See if this is a packet we've recently processed. If it is,
                # immediately send an acknowledgement. Otherwise, add it to the
                # list of requests to process later.
                if (exists($cache{$r->{cookie}})) {
                    my $ack = Petitions::RPC::make_ack_packet($r->{cookie});
                    $s->send($ack, 0, $r->{client});
                        # don't care much if this fails
                } elsif (!exists($pending{$r->{cookie}})) {
                    $pending{$r->{cookie}} = $r;
                    print STDERR ".";
                }

                # Done handling this request.
            } else {
                # Invalid packet of some sort. Log and continue.
                my ($port, $ip) = sockaddr_in($sender);
                print STDERR "bad packet from @{[ inet_ntoa($ip) ]}:$port\n";
            }
        }
        
        if (!$!{EAGAIN}) {
            print STDERR "recvfrom: $!\n";
        }
    }

    # If the alarm has fired, then do the signatures and send the
    # acknowledgements to the clients.
    if ($alarm_fired && keys(%pending)) {
        my $n = scalar(keys(%pending));
        print STDERR "alarm fired; have $n signatures to make\n";
        try {
            do_pending();
            $alarm_fired = 0;
        } catch mySociety::DBHandle::Error with {
            my $E = shift;
            print STDERR "committing ", scalar(keys(%pending)), " requests failed; DB error: ", $E->text(), "\n";
        };
        %pending = ( );
        my $t1 = time();
        printf STDERR "time from first request = %fs; %f sigs/s\n", $t1 - $t0, $n / ($t1 - $t0);
    }
}

if (keys(%pending)) {
    try {
        do_pending();
    } catch mySociety::DBHandle::Error with {
        my $E = shift;
        print STDERR "committing ", scalar(keys(%pending)), " requests failed (at shutdown); DB error: ", $E->text(), "\n";
    };
}

untie(%cache);
