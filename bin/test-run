#!/usr/bin/perl -w -I../../perllib
#
# test-run:
# Test harness for petitions. Makes sure we haven't broken the code.
# 
# Requires:
# * ../general/conf file set up for petitions, and matching the below requirements
# * apache configured to serve ../web on OPTION_BASE_URL
# * a petitions database with name ending "_testharness"; this script will drop and remake 
#   the database, so make sure it is never used for anything important
# * email addresses (email_n below) configured to pipe to ./test-mailin with fast
#   local delivery, and
#
# Copyright (c) 2006 UK Citizens Online Democracy. All rights reserved.
# Email: francis@mysociety.org; WWW: http://www.mysociety.org/
#

# Things to add:
#    - Make petition, reject, amend, accept
#    - Remove signer
#    - RSS feeds
#    - Check rejected petitions in the relevant categories can/can't be seen on the site
#    - Make one of the rejections give multiple reasons

my $rcsid = ''; $rcsid .= '$Id: test-run,v 1.22 2006-10-24 10:47:52 francis Exp $';

use strict;
require 5.8.0;

use mySociety::Util;
use mySociety::Config;
mySociety::Config::set_file('../conf/general');
use mySociety::DBHandle qw(dbh);
use mySociety::WebTestHarness;

use Data::Dumper;
use Carp qw(verbose);
use Storable;
use FindBin;
use Getopt::Long;
use IO::Handle;
use POSIX;

sub help {
print <<END

Usage: test-run [OPTION] [ACTIONS...]

Options are
    --verbose=n   Choose 0 (no progress), 1 (basic actions), 2 (full debug)

Actions are a list of tests, run if present in this order:
    general - create a petition, sign it, let it complete, send government response
    rejection - create a rubbish petition, reject it, let the rejection time out
If you specify no actions, it will run all of them.

END
}

# Parse command line
our $verbose = 0; # currently 3 levels: 0, 1 and 2
our $multispawn = 1; # can test launching multiple instances of cron jobs
our $help;
if (!GetOptions(
        'verbose=i' =>  \$verbose,
        'help' =>               \$help
    )) {
    help();
    exit(1);
}
if ($help) {
    help();
    exit(0);
}
our %action;
my %allowed_actions = ( basic => 1, rejection => 1, timeout => 1 );
foreach (@ARGV) {
    if ($allowed_actions{$_}) {
        $action{$_} = 1;
    } else {
        help();
        print "Action '$_' not known\n";
        exit(0);
    }
}
if (scalar(@ARGV) == 0) {
    %action = %allowed_actions;
}

# Set up options
our $base_url = mySociety::Config::get('BASE_URL');
our $admin_url = mySociety::Config::get('ADMIN_URL');
our $auth_user = mySociety::Config::get('AUTH_USER');
our $auth_password = mySociety::Config::get('AUTH_PASSWORD');
our $httpd_error_log = mySociety::Config::get('HTTPD_ERROR_LOG');
our $email_domain = mySociety::Config::get('EMAIL_DOMAIN');
our $web_domain = mySociety::Config::get('WEB_DOMAIN');
our $contact_email = mySociety::Config::get('CONTACT_EMAIL');
our $test_email_prefix = mySociety::Config::get('TEST_EMAIL_PREFIX');
sub email_n { my $n = shift; return "$test_email_prefix+$n\@$email_domain"; }
sub name_n { my $n = shift; return "Persephone Petitioner $n"; }

#############################################################################
# Main code

# Kill FastCGI processes
# XXX The processes respawn from SIGTERM, and SIGKILL seems to confuse Apache.
# Any way of doing this without needing root access to "apachectl graceful"?
#mySociety::Util::kill_named_processes(SIGTERM, '"^(ref-index.cgi|ref-sign.cgi|php4-cgi)\$"');

# Configure test harness class
print "Set up web test harness...\n" if $verbose > 0;
our $wth = new mySociety::WebTestHarness();
$base_url =~ m#^http://(.+)/?$#;
$wth->browser_credentials("$1:80", "ePetitions staging site", $auth_user, $auth_password); 
$wth->browser_credentials("$1:80", "$web_domain admin pages", $auth_user, $auth_password); 
$wth->database_connect('PET_');
$wth->database_drop_reload('../db/schema.sql');
$wth->database_cycle_sequences(200);
$wth->log_watcher_setup($httpd_error_log);
$wth->log_watcher_self_test($base_url . "/test.php?error=1", "deliberate_error_to_test_error_handling");
my $eveld_bin = "$FindBin::Bin/../../services/EvEl/bin/eveld";
$eveld_bin = undef if ! -e $eveld_bin; # when running on servers rely on EvEl daemon, rather than calling EvEl binary directly XXX need more explicit way of distinguishing this case, than just checking evel isn't checked out in the same tree
$wth->email_setup({ eveld_bin => $eveld_bin,
                    log_mailbox => "log_mailbox" });
$wth->browser_set_validator("/usr/bin/validate");

# Start email and signup daemons
my $petemaild_pid;
my $petsignupd_pid;
END {
    my $exit_code = $?;
    # When script exits, get the email and signup daemons to gracefully shutdown
    kill(POSIX::SIGHUP, $petemaild_pid) if $petemaild_pid;
    kill(POSIX::SIGHUP, $petsignupd_pid) if $petsignupd_pid;
    # Wait for all children to have exited
    while (1) {
        my $wait_pid = wait();
        last if ($wait_pid == -1);
    }
    exit($exit_code);
};
print "Starting daemons...\n" if $verbose > 0;
$petemaild_pid = fork();
die "failed to fork" if (!defined($petemaild_pid));
if ($petemaild_pid == 0) {
    exec("./petemaild", "--debug", "--verbose=0");
    exit;
}
$petsignupd_pid = fork();
die "failed to fork" if (!defined($petsignupd_pid));
if ($petsignupd_pid == 0) {
    exec("./petsignupd", "--debug");
    exit;
}

# Syntax check all .php files
print "Syntax check all PHP files...\n" if $verbose > 0;
$wth->php_check_syntax("../../pet/");
$wth->php_check_syntax("../../pet/templates/emails/", qr//);

# Run tests specified
if ($action{'basic'}) {
    print "Basic test...\n" if $verbose > 0;
    do_basic_test();
}
if ($action{'rejection'}) {
    print "Rejection test...\n" if $verbose > 0;
    do_rejection_test();
}
if ($action{'timeout'}) {
    print "Timeout test...\n" if $verbose > 0;
    do_timeout_test();
}

# Check for any unhandled mails or errors
call_send_messages();
call_mark_finished();
print "Checking no emails left at end...\n" if $verbose > 1;
$wth->email_check_none_left();
print "Checking no log file errors at end...\n" if $verbose > 1;
$wth->log_watcher_check();
print "Everything completed successfully\n";

#############################################################################
# General functions

# Change the date that all parts of ePetitions think is today.  Call with no
# parameters to reset it to the actual today.
sub set_pet_date {
    my $new_date = shift;
    if (defined($new_date)) {
        dbh()->do('delete from debugdate; insert into debugdate (override_today) values (?);', {}, $new_date);
    } else {
        dbh()->do('delete from debugdate;');
    }
    dbh()->commit();
}

# Call send messages cron job
sub call_send_messages {
     $wth->multi_spawn($multispawn, "php send-messages " . ($verbose > 1 ? qw(--verbose) : ''), $verbose);
}

# Call mark finished cron job
sub call_mark_finished {
     $wth->multi_spawn($multispawn, "php mark-finished " . ($verbose > 1 ? qw(--verbose) : ''), $verbose);
}

#############################################################################

# Create petition, sign it, wait until completion, send government response
sub do_basic_test {
    set_pet_date('1988-08-08');

    my $petition_text = "take action on every petition on this site with more than 1000 signers";

    # Check front page works
    $wth->browser_get($base_url);
    $wth->browser_check_contents("Petitions");
    $wth->browser_check_no_contents("0 signatures");

    # Create a new petition
    $wth->browser_follow_link(text_regex => qr/Create a Petition/);
    $wth->browser_check_contents("Part 1 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => {
                pet_content => $petition_text,
                detail => "I think it is very important that the Prime Minister takes all petitions seriously. He should be compelled to do anything that more than 1000 people sign, as this would be good for democracy and not at all exploitable by small groups of people.\n\nMy favourite thing about this petition is that it has two paragraphs of text in the description. That rocks!",
                rawdeadline => 'Dec 31st',
                ref => 'onethousand'
                }, button => 'tostepyou') or die "Failed to submit step 1";

    $wth->browser_check_contents("Part 2 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => {
                name => name_n(99),
                organisation => "Society for the Advancement of Effective Petitioning",
                address => "The Beseechment Building\nImportuning Island",
                postcode => "SW1A 2AA",
                telephone => "01234 56789",
                org_url => "http://www.petitionsrock.org.uk",
                email => email_n(99),
                email2 => email_n(99)
                }, button => 'tosteppreview') or die "Failed to submit step 2";

    $wth->browser_check_contents("Part 3 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => { }, button => 'tocreate') or die "Failed to submit step 3";

    $wth->browser_check_contents("We have sent you an email to confirm");

    # Confirm petition
    my $confirm_email = $wth->email_get_containing('%Subject: Confirm your new petition%To: "'.name_n(99).'" <'.email_n(99).'>%');
    die "Confirm petition email link not found\n" if ($confirm_email !~ m#^\s*(https?://.*$)#m);
    my $confirm_petition_link = $1;
    print "Confirmation link: $confirm_petition_link\n" if $verbose > 1;
    $wth->browser_get($confirm_petition_link);
    $wth->browser_check_contents("Thank you for creating your petition");
    $wth->browser_check_contents("It has been entered on our system");

    # Get the email to the administrator
    call_send_messages();
    my $admin_approve_email = $wth->email_get_containing('%Subject: New petition to the Prime Minister%To: '.$contact_email.'%');
    die "Admin approve email link not found\n" if ($admin_approve_email !~ m#^\s*(http://.*$)#m);
    my $admin_approve_link = $1;
    print "Admin approve link: $admin_approve_link\n" if $verbose > 1;

    # Follow link to admin page
    $wth->browser_get($admin_approve_link);
    $wth->browser_check_contents("Petitions and Signers");
    $wth->browser_check_contents($petition_text);
    $wth->email_check_none_left();
    call_send_messages();
    $wth->browser_submit_form(form_name => 'petition_admin_approve',
            fields => { }, button => 'approve') or die "Failed to submit approve petition button";
    call_send_messages();
    my $petition_approved_email = $wth->email_get_containing('%Subject: Your petition to the Prime Minister has been approved%To: '.email_n(99).'%');
    $wth->browser_check_contents('Petition approved!');

    # Check that petition is on the front page, and has its own page
    $wth->browser_get($base_url);
    $wth->browser_check_contents($petition_text);
    $wth->browser_check_contents("0 signatures");
    $wth->browser_follow_link(text_regex => qr/$petition_text/);
    $wth->browser_check_contents($petition_text);
    $wth->browser_check_contents("31 December 1988");

    # Loop through several signers
    my $c = 0;
    foreach my $signer_no (1488..1490) {
        my $uri = $wth->browser_uri();

        # Sign the petition
        $wth->browser_submit_form(form_name => 'signForm',
                fields => {
                    name => name_n($signer_no),
                    email => email_n($signer_no),
                    email2 => email_n($signer_no),
                    address => "$signer_no Signature Street\n\nSealville",
                    postcode => "EH99 1SP",
                    }, button => 'submit') or die "Failed to submit sign petition";
        $wth->browser_check_contents("We've sent you an email");

        # Find confirmation email
        my $confirm_signature_email = $wth->email_get_containing('%Subject: Signing up to ask the Prime Minister%To: "'.name_n($signer_no).'" <'.email_n($signer_no).'>%Please click on the link below to confirm your signature%');
        die "Confirm signature email link not found\n" if ($confirm_signature_email !~ m#^\s*(https?://.*$)#m);
        my $confirm_signature_link = $1;
        if ($signer_no == 1490) {
            # Confirm signature by administraor
            $wth->browser_get($admin_approve_link);
            $wth->browser_check_contents("Petitions and Signers");
            $wth->browser_check_no_contents($petition_text);
            $wth->browser_follow_link(text_regex => qr/Live/);
            $wth->browser_check_contents($petition_text);
            $wth->browser_follow_link(text_regex => qr/admin/);
            $wth->browser_check_contents($petition_text);
            $wth->browser_submit_form(form_name => 'removesignerform1',
                fields => { }, button => 'confirm_signer') or die "Failed to submit admin confirm signer button";
            $wth->browser_check_contents("That signer has been confirmed");
            $wth->browser_check_contents("Signers \\(3\\)");
            $wth->browser_check_no_contents("confirm_signer");
        } else {
            # Confirm signature by user clicking link
            print "Confirmation link: $confirm_signature_link\n" if $verbose > 1;
            $wth->browser_get($confirm_signature_link);
            $wth->browser_check_contents("Thank you, you're now signed up to this petition!");
        }

        # Check it says it is signed on the front page
        $wth->browser_get($base_url);
        $wth->browser_check_contents($petition_text);
        $c++;
        $wth->browser_check_contents("$c signature");

        $wth->browser_get($uri);
    }

    # Go forward in time to day that deadline expires at end of, and
    # make sure that nothing happens.
    set_pet_date('1988-12-31');
    call_mark_finished();
    $wth->browser_get($base_url);
    $wth->browser_check_contents($petition_text);
    $wth->email_check_none_left();

    # And one more day, check pledge is marked as finished
    set_pet_date('1989-01-01');
    call_mark_finished();
    # XXX probably should be a "your pledge has finished" mail here
    $wth->browser_get($base_url);
    $wth->browser_check_no_contents($petition_text);
    $wth->browser_check_contents("None");
    $wth->browser_follow_link(text_regex => qr/View petitions/);
    $wth->browser_check_no_contents($petition_text);
    $wth->browser_follow_link(text_regex => qr/Closed petitions/);
    $wth->browser_check_contents($petition_text);
    $wth->browser_follow_link(text_regex => qr/$petition_text/);
    $wth->browser_check_contents("This petition is now closed");

    # Send a response to all the signers
    $wth->browser_get($admin_url);
    $wth->browser_follow_link(text_regex => qr/Petitions and Signers/);
    $wth->browser_follow_link(text_regex => qr/Finished/);
    $wth->browser_check_no_contents('Response sent');
    $wth->browser_submit_form(form_name => 'petition_admin_respond',
                fields => { }, button => 'respond') or die "Failed to submit write response button";
    $wth->browser_submit_form(form_name => 'petition_admin_respond',
                fields => {
                    message_body => "Thank you for your petition. We are going to do exactly what you say, because obviously we would never get contradictory petitions from different people. Yours faithfully, the PM.",
                    }, button => 'respond') or die "Failed to submit response form";
    $wth->browser_check_contents("Your response has been recorded and will be sent out shortly");
    call_send_messages();

    # Check everyone gets the response; signers, creator and admin
    foreach my $signer_no (1488..1490) {
        $wth->email_get_containing('%Subject: Government response%To: '.email_n($signer_no).'%Thank you for your petition%');
    }
    $wth->email_get_containing('%Subject: Government response%To: '.email_n(99).'%Thank you for your petition%');
    $wth->email_get_containing('%Subject: Government response%To: '.$contact_email.'%Thank you for your petition%');

    # Check that can't send another response
    $wth->browser_get($admin_url);
    $wth->browser_follow_link(text_regex => qr/Petitions and Signers/);
    $wth->browser_follow_link(text_regex => qr/Finished/);
    $wth->browser_check_contents('Response sent');
    $wth->browser_check_no_contents('petition_admin_respond');
}

# Make a petition, and reject it, resubmit, and reject again
sub do_rejection_test {
    set_pet_date('1989-03-01');

    my $petition_text = "impeach President Bush";

    # Create a new petition
    $wth->browser_get($base_url);
    $wth->browser_follow_link(text_regex => qr/Create a Petition/);
    $wth->browser_check_contents("Part 1 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => {
                pet_content => $petition_text,
                detail => "Isn't it obvious! I have no understanding of the powers of a Prime Minister! I'm going to dumbly ask him to do something that he can't do!",
                rawdeadline => '1 week',
                ref => 'rejecttest'
                }, button => 'tostepyou') or die "Failed to submit step 1";

    $wth->browser_check_contents("Part 2 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => {
                name => name_n(44),
                organisation => "Crazy Pressure Group",
                address => "Removal From Office Rd.",
                postcode => "SW1A 2AB",
                telephone => "09876 5432",
                org_url => "http://www.impeachbush.org",
                email => email_n(44),
                email2 => email_n(44)
                }, button => 'tosteppreview') or die "Failed to submit step 2";

    $wth->browser_check_contents("Part 3 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => { }, button => 'tocreate') or die "Failed to submit step 3";

    $wth->browser_check_contents("We have sent you an email to confirm");

    # Confirm petition
    my $confirm_email = $wth->email_get_containing('%Subject: Confirm your new petition%To: "'.name_n(44).'" <'.email_n(44).'>%');
    die "Confirm petition email link not found\n" if ($confirm_email !~ m#^\s*(https?://.*$)#m);
    my $confirm_petition_link = $1;
    print "Confirmation link: $confirm_petition_link\n" if $verbose > 1;
    $wth->browser_get($confirm_petition_link);
    $wth->browser_check_contents("Thank you for creating your petition");
    $wth->browser_check_contents("It has been entered on our system");

    # Get the email to the administrator
    call_send_messages();
    my $admin_approve_email = $wth->email_get_containing('%Subject: New petition to the Prime Minister%To: '.$contact_email.'%');
    die "Admin approve email link not found\n" if ($admin_approve_email !~ m#^\s*(http://.*$)#m);
    my $admin_approve_link = $1;
    print "Admin approve link: $admin_approve_link\n" if $verbose > 1;

    # Follow link to admin page
    $wth->browser_get($admin_approve_link);
    $wth->browser_check_contents("Petitions and Signers");
    $wth->browser_check_contents($petition_text);
    $wth->email_check_none_left();
    call_send_messages();
    $wth->browser_submit_form(form_name => 'petition_admin_approve',
            fields => { }, button => 'reject') or die "Failed to submit reject petition button";
    $wth->browser_check_contents("You have chosen to reject the petition 'rejecttest'");
    $wth->browser_submit_form(form_name => 'rejection_details_form',
            fields => { reason => 'Sorry, this is something the US Congress can do, but not the Prime Minster of the UK! You might want to change your petition to ask the Prime Minister to write a public letter to the Congress leader to ask him to impeach Bush, or something like that.',
                'categories[]' => ['4096', 13], # the 13th checkbox has value 4096, and means "PM doesn't have power"
            }, button => 'reject_submit') or die "Failed to submit reject details form";
    call_send_messages();
    my $rejection_email = $wth->email_get_containing('%Subject: Your petition has been rejected%To: '.email_n(44).'%Outside the remit or powers%');
    die "Rejection email link not found\n" if ($rejection_email !~ m#^\s*(https?://.*$)#m);
    my $rejection_email_link = $1;
    print "Rejection resubmit link: $rejection_email_link\n" if $verbose > 1;
   
    # Resubmit with change in petition text
    my $petition_text_resubmitted = "bleach President Bush";
    $wth->browser_get($rejection_email_link);
    $wth->browser_check_contents($petition_text);
    $wth->browser_check_contents('Choose a short name for your petition');
    $wth->browser_check_contents("Part 1 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => { pet_content => $petition_text_resubmitted }, button => 'tostepyou') or die "Failed to submit step 1";
    $wth->browser_check_contents("Part 2 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => { }, button => 'tosteppreview') or die "Failed to submit step 2";
    $wth->browser_check_contents("Part 3 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => { }, button => 'tocreate') or die "Failed to submit step 3";
    $wth->browser_check_contents("We have resubmitted your petition for approval.");

    # Get the email to the administrator
    call_send_messages();
    $admin_approve_email = $wth->email_get_containing('%Subject: Resubmitted petition to the Prime Minister%To: '.$contact_email.'%');
    die "Admin approve email link not found\n" if ($admin_approve_email !~ m#^\s*(http://.*$)#m);
    $admin_approve_link = $1;
    print "Admin approve link: $admin_approve_link\n" if $verbose > 1;

    # Follow link to admin page
    $wth->browser_get($admin_approve_link);
    $wth->browser_check_contents("Petitions and Signers");
    $wth->browser_check_no_contents($petition_text);
    $wth->browser_check_contents($petition_text_resubmitted);
    $wth->email_check_none_left();
    call_send_messages();
    $wth->browser_submit_form(form_name => 'petition_admin_approve',
            fields => { }, button => 'reject') or die "Failed to submit reject petition button";
    $wth->browser_check_contents("You have chosen to reject the petition 'rejecttest'");
    $wth->browser_submit_form(form_name => 'rejection_details_form',
            fields => { reason => 'It is just nonsense, sorry!',
                'categories[]' => ['256', 9], # the 9th checkbox has value 256, and means "Not clear what asking signers to endorse"
            }, button => 'reject_submit') or die "Failed to submit reject details form";
    call_send_messages();
    $rejection_email = $wth->email_get_containing('%Subject: Your petition has been rejected%To: '.email_n(44).'%clear what the petition%');
    die "Rejection email link found when it should not have been\n" if ($rejection_email =~ m#^\s*(https?://.*$)#m);

    # Check appears on rejection page
    $wth->browser_get($base_url);
    $wth->browser_follow_link(text_regex => qr/View petitions/);
    $wth->browser_follow_link(text_regex => qr/Rejected petitions/);
    $wth->browser_check_contents($petition_text_resubmitted);
    $wth->browser_follow_link(text_regex => qr/$petition_text_resubmitted/);
    $wth->browser_check_contents("This petition has been <strong>rejected</strong>");
}

# Test timeout from rejectedonce to rejected
sub do_timeout_test {
    set_pet_date('1989-06-01');

    my $petition_text = "say crap on the radio";

    # Create a new petition
    $wth->browser_get($base_url);
    $wth->browser_follow_link(text_regex => qr/Create a Petition/);
    $wth->browser_check_contents("Part 1 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => {
                pet_content => $petition_text,
                detail => "Oh flip oh gosh oh golly gee. We said the word and got our knuckles rapped.",
                rawdeadline => '1 month',
                ref => 'timeouttest'
                }, button => 'tostepyou') or die "Failed to submit step 1";

    $wth->browser_check_contents("Part 2 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => {
                name => name_n(55),
                organisation => "Stiff Little Fingers",
                address => "Uncle John's house",
                postcode => "WC2H 7LA",
                telephone => "02143 6587",
                org_url => "http://www.blablahblah",
                email => email_n(55),
                email2 => email_n(55)
                }, button => 'tosteppreview') or die "Failed to submit step 2";

    $wth->browser_check_contents("Part 3 of 3");
    $wth->browser_submit_form(form_name => 'newpetition',
            fields => { }, button => 'tocreate') or die "Failed to submit step 3";

    $wth->browser_check_contents("We have sent you an email to confirm");

    # Confirm petition
    my $confirm_email = $wth->email_get_containing('%Subject: Confirm your new petition%To: "'.name_n(55).'" <'.email_n(55).'>%');
    die "Confirm petition email link not found\n" if ($confirm_email !~ m#^\s*(https?://.*$)#m);
    my $confirm_petition_link = $1;
    print "Confirmation link: $confirm_petition_link\n" if $verbose > 1;
    $wth->browser_get($confirm_petition_link);
    $wth->browser_check_contents("Thank you for creating your petition");
    $wth->browser_check_contents("It has been entered on our system");

    # Get the email to the administrator
    call_send_messages();
    my $admin_approve_email = $wth->email_get_containing('%Subject: New petition to the Prime Minister%To: '.$contact_email.'%');
    die "Admin approve email link not found\n" if ($admin_approve_email !~ m#^\s*(http://.*$)#m);
    my $admin_approve_link = $1;
    print "Admin approve link: $admin_approve_link\n" if $verbose > 1;

    # Follow link to admin page
    $wth->browser_get($admin_approve_link);
    $wth->browser_check_contents("Petitions and Signers");
    $wth->browser_check_contents($petition_text);
    $wth->email_check_none_left();
    call_send_messages();
    $wth->browser_submit_form(form_name => 'petition_admin_approve',
            fields => { }, button => 'reject') or die "Failed to submit reject petition button";
    $wth->browser_check_contents("You have chosen to reject the petition 'timeouttest'");
    $wth->browser_submit_form(form_name => 'rejection_details_form',
            fields => { reason => 'You can\'t say that on our website either.',
                'categories[]' => ['128', 8], # the 8th checkbox has value 128, and means "offensive language"
            }, button => 'reject_submit') or die "Failed to submit reject details form";
    call_send_messages();
    my $rejection_email = $wth->email_get_containing('%Subject: Your petition has been rejected%To: '.email_n(55).'%Offensive language%');
    die "Rejection email link not found\n" if ($rejection_email !~ m#^\s*(https?://.*$)#m);
    my $rejection_email_link = $1;
    print "Rejection resubmit link: $rejection_email_link\n" if $verbose > 1;
    # Do not resubmit

    # Just before timeout
    set_pet_date('1989-06-29');
    call_mark_finished();
    call_send_messages();
    # ... make sure not rejected yet
    $wth->email_check_none_left();
    $wth->browser_get($base_url);
    $wth->browser_follow_link(text_regex => qr/View petitions/);
    $wth->browser_follow_link(text_regex => qr/Rejected petitions/);
    $wth->browser_check_no_contents($petition_text);
    # Just after timeout
    set_pet_date('1990-06-30'); 
    call_mark_finished();
    call_send_messages();
    $wth->email_get_containing('%Subject: Your petition has been finally rejected%To: '.email_n(55).'%%');

    # Make on rejection page
    $wth->browser_get($base_url);
    $wth->browser_follow_link(text_regex => qr/View petitions/);
    $wth->browser_follow_link(text_regex => qr/Rejected petitions/);
    $wth->browser_check_contents($petition_text);
    $wth->browser_follow_link(text_regex => qr/$petition_text/);
    $wth->browser_check_contents("This petition has been <strong>rejected</strong>");
}
   
#############################################################################


