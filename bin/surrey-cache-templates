#!/usr/bin/perl -w
# 
# surrey-cache-templates
# 
# This script will run daily overnight to fetch and cache various
# council headers and footers to be used for the Surrey petitions website.

# TODO: Add CSS and RSS links in headers.

use strict;
use File::Slurp;
use FindBin;
use LWP::Simple;

use lib "$FindBin::Bin/../perllib";
use lib "$FindBin::Bin/../commonlib/perllib";
 
use mySociety::Config;
BEGIN {
    mySociety::Config::set_file("$FindBin::Bin/../conf/general");
}
use mySociety::DBHandle qw(dbh);
use mySociety::Memcached;
mySociety::Memcached::set_config(mySociety::Config::get('MEMCACHE_HOST'), mySociety::Config::get('PET_DB_NAME'));
use Petitions;

my ($header, $footer);
my $template_root = "$FindBin::Bin/../templates";

# Surrey
my $header1 = get_page('http://build.surreycc.gov.uk/web-templates/e-petitions?SQ_DESIGN_NAME=head');
my $header2 = get_page('http://build.surreycc.gov.uk/web-templates/e-petitions?SQ_DESIGN_NAME=body_head');

if ($header1 && $header2) {
    $header = $header1;
    $header .= <<EOF;
<link rel="stylesheet" type="text/css" href="/pet.css" />
<link rel="stylesheet" type="text/css" href="/assets/surreycc/css.css" />
EOF
    $header .= $header2;
    $header =~ s/(<title>Surrey County Council -) .*?(<\/title>)/$1 PARAM_TITLE$2/;
    $header =~ s/<link rel="alternate" type="application\/rss+xml".*? \/>/PARAM_RSS_LINKS\nPARAM_SUBJECTS/;
    $header =~ s/<link rel="alternate" type="application\/rss+xml".*? \/>//g;
    $header =~ s/(<meta name="DC\.Identifier".*?")http.*?"/$1PARAM_DC_IDENTIFIER"/;
    $header .= "\n<h1>PARAM_H1</h1>\n";
}

$footer = get_page('http://build.surreycc.gov.uk/web-templates/e-petitions?SQ_DESIGN_NAME=body_footer');
output('surreycc', $header, $footer);

# Surrey Heath
$header = get_page('http://www.surreyheath.gov.uk/general_header.asp');
$header =~ s/<title>/<title>PARAM_TITLE - /;
$header =~ s/<\*\*page_title\*\*>/PARAM_H1/;
$header =~ s{<span class="current">Home: </span>}{<a title="Breadcrumb link: Home" href="http://www.surreyheath.gov.uk/default.htm">Home</a> &gt; <a title="Breadcrumb link: Council &amp; Democracy" href="http://www.surreyheath.gov.uk/council/default.htm">Council &amp; Democracy</a> &gt; <a href="/">Petitions</a> &gt; <span class="current">PARAM_H1</span>};
$header =~ s{</head>}{<link rel="stylesheet" type="text/css" href="/pet.css" />
<link rel="stylesheet" type="text/css" href="/assets/surreyheath/css.css" />
</head>};
$footer = get_page('http://www.surreyheath.gov.uk/general_footer.asp');
output('surreyheath', $header, $footer);

# Elmbridge
$header = get_page('http://www.elmbridge.gov.uk/website_header.asp');
$header =~ s/<title>/<title>PARAM_TITLE - /;
$header =~ s/iso-8859-1/utf-8/; # Sigh
$header =~ s{</head>}{<link rel="stylesheet" type="text/css" href="/pet.css" />
<link rel="stylesheet" type="text/css" href="/assets/elmbridge/css.css" />
</head>};
$header .= '&gt; <a href="/">E-petitions</a> &gt; PARAM_H1';
$header .= "\n<div id='mysociety'>\n";
$footer = "</div>\n";
$footer .= get_page('http://www.elmbridge.gov.uk/website_footer.asp');
output('elmbridge', $header, $footer);

# Tandridge
$header = get_page('http://www.tandridge.gov.uk/petitions_header.asp');
$header =~ s/<title>/<title>PARAM_TITLE - Tandridge District Council/;
$header =~ s{</head>}{<link rel="stylesheet" type="text/css" href="/pet.css" />
<link rel="stylesheet" type="text/css" href="/assets/tandridge/css.css" />
</head>};
$header .= "<div id='mysociety'>\n";
$footer = "</div>\n";
$footer .= get_page('http://www.tandridge.gov.uk/petitions_footer.asp');
output('tandridge', $header, $footer);

sub output {
    my ($dir, $header, $footer) = @_;

    my $flush = 0;

    my $template_dir = "$template_root/$dir";
    mkdir($template_dir);

    if ($header) {
        my $current_header = read_file("$template_dir/head.html", err_mode => 'quiet');
        if ($current_header ne $header) {
            write_file("$template_dir/head.html", { atomic => 1 }, \$header);
            $flush = 1;
        }
    }
    if ($footer) {
        my $current_footer = read_file("$template_dir/foot.html", err_mode => 'quiet');
        if ($current_footer ne $footer) {
            write_file("$template_dir/foot.html", { atomic => 1 }, \$footer);
            $flush = 1;
        }
    }

    if ($flush) {
        my $ids = dbh()->selectcol_arrayref('select id from petition where body_id=(select id from body where ref=?)', {}, $dir);
        foreach (@$ids) {
            mySociety::Memcached::set("lastupdate:$_", time());
        }
    }
}

sub get_page {
    my $url = shift;
    my $r = LWP::Simple::get($url);
    return $r || '';
}
